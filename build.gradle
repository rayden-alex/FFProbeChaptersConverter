plugins {
    id 'java'
}

group = 'by.rayden'
version = '1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

configurations {
    mockitoAgent
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2-1'

    implementation platform('tools.jackson:jackson-bom:3.0.4')
    implementation 'tools.jackson.core:jackson-databind'
    implementation 'org.digitalmediaserver:cuelib-core:2.0.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'commons-cli:commons-cli:1.11.0'

    testImplementation platform('org.junit:junit-bom:6.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
//    testImplementation("org.junit-pioneer:junit-pioneer:2.3.0") {
//        capabilities {
//            requireCapability("org.junit-pioneer:junit-pioneer-jackson") // Uses Jackson2 implementation :(
//        }
//    }

    testImplementation 'org.assertj:assertj-core:3.27.7'

    testImplementation 'org.mockito:mockito-core:5.21.0'
    mockitoAgent('org.mockito:mockito-core:5.21.0') {
        transitive = false
    }
}

jar {
    // Locking the file name to avoid changing it every time in cmd-scripts
    archiveFileName = 'FFProbeChaptersConverter.jar'

    manifest {
        attributes(
                'Main-Class': 'by.rayden.ffprobechaptersconverter.FFProbeChaptersConverter',
                'Build-Revision': '1.2.0'
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.register('generateAotCache', JavaExec) {
    group = 'build'
    description = 'Generates AOT Cache for the Java application'

    // Depending on the JAR build.
    dependsOn tasks.jar

    // Dynamically get the launcher from the project's toolchain settings to ensure
    // that javac (compilation) and java (cache generation in the generateAotCache task)
    // use the same JDK 25 binary file.
    def service = project.extensions.getByType(JavaToolchainService)
    javaLauncher = service.launcherFor(java.toolchain)

    // Setting up a training run:
    // take a freshly assembled JAR and specify the Main class.
    classpath = files(tasks.jar.archiveFile)
    mainClass = 'by.rayden.ffprobechaptersconverter.FFProbeChaptersConverter'

    // Java 25 AOT-Cache write options
    jvmArgs = [
            "-XX:AOTCacheOutput=${layout.buildDirectory.get()}\\libs\\FFProbeChaptersConverter.aot",
            "-XX:+UseCompactObjectHeaders"
    ]

    // Training run (must end with code 0).
    // The CLI must do minimal work and shut down to save the cache.
    args = ['--help']
}

// Link "generateAotCache" task to the build cycle: the cache will be created immediately after the "build"
tasks.named('build') {
    finalizedBy 'generateAotCache'
}

test {
    useJUnitPlatform()

    // to suppress WARNING like this:
    // "A Java agent has been loaded dynamically...  byte-buddy-agent...",
    // "OpenJDK 64-Bit Server VM warning: Sharing is only supported for boot loader classes
    // because bootstrap classpath has been appended"
    jvmArgs += '-Xshare:off'
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"

    // To show passed test in console output
//    testLogging {
//        events "passed", "skipped", "failed" // , "standardOut", "standardError"
//   }

}
